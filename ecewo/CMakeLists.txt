cmake_minimum_required(VERSION 3.10)
project(ecewo VERSION 0.17.0 LANGUAGES C)

# Platform definitions
if(WIN32)
    add_definitions(-DPLATFORM_WINDOWS)
elseif(APPLE)
    add_definitions(-DPLATFORM_MACOS)
elseif(UNIX)
    add_definitions(-DPLATFORM_LINUX)
endif()

include(FetchContent)

# Fetch libuv from the GitHub repository
FetchContent_Declare(
  libuv
  GIT_REPOSITORY https://github.com/libuv/libuv.git
  GIT_TAG v1.51.0
)

# Make libuv available to the project
FetchContent_MakeAvailable(libuv)

# Fetch jansson from the GitHub repository
FetchContent_Declare(
    jansson
    GIT_REPOSITORY https://github.com/akheron/jansson.git
    GIT_TAG v2.14.1
)

FetchContent_MakeAvailable(jansson)

# Fetch llhttp from the GitHub repository
FetchContent_Declare(
  llhttp
  URL "https://github.com/nodejs/llhttp/archive/refs/tags/release/v8.1.0.tar.gz"
)

# Disable shared library build and enable static library build
set(BUILD_SHARED_LIBS OFF CACHE INTERNAL "")
set(BUILD_STATIC_LIBS ON CACHE INTERNAL "")
set(JANSSON_BUILD_DOCS OFF CACHE BOOL "Disable Jansson documentation" FORCE)
set(JANSSON_WITHOUT_TESTS ON CACHE BOOL "Disable Jansson tests" FORCE)

# Make llhttp available to the project
FetchContent_MakeAvailable(llhttp)

# List of source files
set(SRC_FILES
    router.c
    server/handler.c
    server/request.c
    server/start/ecewo.c
    server/utils/session.c
    server/utils/middleware.c
    server/utils/async.c
    server/lib/dotenv.c
)

# Create library
add_library(ecewo STATIC ${SRC_FILES})

# Include directories
target_include_directories(ecewo PUBLIC
    ${CMAKE_CURRENT_SOURCE_DIR}/..
    ${CMAKE_CURRENT_SOURCE_DIR}
    ${CMAKE_CURRENT_SOURCE_DIR}/config
    ${CMAKE_CURRENT_SOURCE_DIR}/server
    ${CMAKE_CURRENT_SOURCE_DIR}/server/start
    ${CMAKE_CURRENT_SOURCE_DIR}/server/utils
    ${CMAKE_CURRENT_SOURCE_DIR}/server/lib
    ${CMAKE_BINARY_DIR}/libuv/include
    ${CMAKE_BINARY_DIR}/llhttp/include
    ${CMAKE_BINARY_DIR}/jansson/include
    ${CMAKE_BINARY_DIR}/_deps/jansson-src/src
    ${CMAKE_BINARY_DIR}/_deps/jansson-build/include
    ${CMAKE_BINARY_DIR}
)

# Create configuration file
set(ECEWO_COPYRIGHT "2025 Â© Savas Sahin <savashn>")

configure_file(
    "${CMAKE_CURRENT_SOURCE_DIR}/config/config.h.in"
    "${CMAKE_BINARY_DIR}/ecewo_config.h"
)

# Link libraries
target_link_libraries(ecewo uv jansson llhttp_static)

# Link additional libraries for Windows
if(WIN32)
    target_link_libraries(ecewo wsock32 ws2_32)
endif()

# Compiler settings
if(MSVC)
    # Settings for Visual Studio
    target_compile_options(ecewo PRIVATE /W4)
    add_definitions(-D_CRT_SECURE_NO_WARNINGS)
else()
    # Settings for GCC/Clang
    target_compile_options(ecewo PRIVATE -Wall -Wextra -std=gnu11)
endif()

# Debug information
message(STATUS "Configuring ecewo version ${ecewo_VERSION}")
message(STATUS "Target platform: ${CMAKE_SYSTEM_NAME}")
